OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	_talk = OnTranslateInternal.AutoPauses(_talk)
	TOSTR(_talk)
}

OnTranslateInternal.AutoPauses
{
	_talk = _argv[0]
	
	if "\![autopause]" _in_ _talk
	{	
		_talk = REPLACE(_talk,", ",",\w4 ") //Comma
		_talk = REPLACE(_talk,". ",".\w8 ") //Period
		_talk = REPLACE(_talk,"? ","?\w8 ") //Question mark
		_talk = REPLACE(_talk,"! ","!\w8 ") //Exclamation point
		//_talk = REPLACE(_talk,"- ","-\w8 ") //Dash - This one seems to happen a lot when I don't want it, so I am commenting it out
		_talk = REPLACE(_talk,"– ","–\w8\w8 ") //Endash
		_talk = REPLACE(_talk,"; ",";\w8 ") //Semicolon
		_talk = REPLACE(_talk,"~ ","~\w8 ") //Tilde
		_talk = REPLACE(_talk,"\n\n ","\n\n\w8\w8") //Linebreak
	}
	
	_talk
}

OnTextDrop
{
	if SHIORI3FW.DebugMode == 1; OnTranslateInternal.AutoPauses("\0%(s0)\f[italic,1]\![autopause]%(reference0)")
}


On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/call_of_the_sea/main/"
}

endash
{
	"\f[name,calibri]–\f[name,default]"
}

emdash
{
	"\f[name,calibri]—\f[name,default]"
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t" && (ASEARCH(CurrentStory,CompletedStories) != -1 || CurrentStory == ""); OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

s0
{
	"\s[%(Shell)0]"
}

s1
{
	"\s[%(Shell)1]"
}

Capitalize
{
	//The first part here is getting the first letter and making it capital, and the second just gets everything else as it is. It concatenates those two parts, then returns them.
	TOUPPER(SUBSTR(_argv[0],0,1)) + SUBSTR(_argv[0],1,STRLEN(_argv[0]))
}

OnSurfaceRestore
{
	"\1\s[-1]\0"
	--
	if FinaleEnd == 1; "%(s1)"
	else; "%(s0)"
}

br
{
	if SHIORI3FW.BalloonName == "Beach Day Gone Wrong"; "\n"
	else; " "
}

//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Beach sounds";	"https://mynoise.net/NoiseMachines/pebbleBeachNoiseGenerator.php"
	"Ocean sounds";	"https://mynoise.net/NoiseMachines/finWhaleUnderwaterSoundscapeGenerator.php"
}


On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Zichqec";	"https://zichqec.github.io/s-the-skeleton/"
}
